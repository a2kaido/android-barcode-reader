apply plugin: "com.android.application"

apply plugin: "kotlin-android"

apply plugin: "kotlin-kapt"

apply plugin: "androidx.navigation.safeargs"

apply plugin: 'com.google.android.gms.oss-licenses-plugin'

def major = 1
def minor = 3
def revision = 2

android {
    compileSdk 34
    defaultConfig {
        applicationId "io.github.a2kaido.barcode.reader"
        minSdkVersion 25
        targetSdkVersion 34
        versionCode major * 10000 + minor * 100 + revision
        versionName "$major.$minor.$revision"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "ja", "en"
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix = ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    namespace 'io.github.a2kaido.barcode.reader'

    buildFeatures {
        viewBinding = true
        compose = true
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.2"
    }
}

dependencies {
    implementation project(":domain")
    implementation project(":data")
    implementation project(":emvco")
    implementation project(":feature_history")
    implementation project(":feature_settings")
    implementation project(":ui_common")

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7"

    // compose
    def composeBom = platform("androidx.compose:compose-bom:2024.02.01")
    implementation(composeBom)
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui"

    // koin
    def koin_version = "1.0.2"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // support
    implementation "androidx.appcompat:appcompat:1.3.0-alpha02"
    implementation "com.google.android.material:material:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.1"

    // nav
    def nav_version = "2.7.6"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // room
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-coroutines:2.1.0-alpha04"

    // barcode
    implementation("com.journeyapps:zxing-android-embedded:3.6.0") { transitive = false }.because("barcode reader")
    implementation 'com.google.zxing:core:3.4.0'

    // groupie
    implementation "com.xwray:groupie:2.8.1"
    implementation "com.xwray:groupie-viewbinding:2.8.1"

    // permission dispatcher
    def permission_dispatcher_version = "4.9.2"
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:${permission_dispatcher_version}"
    kapt "com.github.permissions-dispatcher:permissionsdispatcher-processor:${permission_dispatcher_version}"

    // stetho
    implementation 'com.facebook.stetho:stetho:1.5.1'

    // OSS license page
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"

    // In-App review
    implementation 'com.google.android.play:core:1.8.0'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // test
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    testImplementation "androidx.room:room-testing:$room_version"
}
