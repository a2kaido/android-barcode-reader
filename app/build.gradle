apply plugin: "com.android.application"

apply plugin: "kotlin-android"

apply plugin: "kotlin-kapt"

apply plugin: "kotlin-android-extensions"

apply plugin: "androidx.navigation.safeargs"

apply plugin: 'com.google.gms.oss.licenses.plugin'

def major = 1
def minor = 1
def revision = 0

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.a2kaido.barcode.reader"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode major * 10000 + minor * 100 + revision
        versionName "$major.$minor.$revision"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "ja", "en"
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix = ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    bundle {
        language {
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation project(":domain")
    implementation project(":data")
    implementation project(":emvco")

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1"

    // koin
    def koin_version = "1.0.2"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // support
    implementation "androidx.appcompat:appcompat:1.1.0-beta01"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta2"

    // nav
    def nav_version = "1.0.0"
    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"

    // room
    def room_version = "2.1.0-alpha04"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-coroutines:$room_version"

    // barcode
    implementation("com.journeyapps:zxing-android-embedded:3.6.0") { transitive = false }.because("barcode reader")
    implementation "com.google.zxing:core:3.3.2"

    // groupie
    implementation "com.xwray:groupie:2.3.0"
    implementation "com.xwray:groupie-kotlin-android-extensions:2.3.0"

    // permission dispatcher
    def permission_dispatcher_version = "4.3.0"
    implementation "org.permissionsdispatcher:permissionsdispatcher:${permission_dispatcher_version}"
    kapt "org.permissionsdispatcher:permissionsdispatcher-processor:${permission_dispatcher_version}"

    // stetho
    implementation "com.facebook.stetho:stetho:1.5.0"

    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"

    // test
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    testImplementation "androidx.room:room-testing:$room_version"
}
